plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
//	id 'org.flywaydb.flyway' version '9.22.3'  // PostgreSQL 14.12와 호환되는 Flyway 버전
}

group = 'com.polarbookshop'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

description = 'Provides functionality for managing the books in the catalog.'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2024.0.0")	// 사용할 스프링 클라우드 버전 정의
	set('testcontainersVersion', "1.19.8")	// PostgreSQL 데이터베이스에 대한 컨테이너 관리 기능을 제공
}

bootBuildImage {		// 빌드팩을 사용해 OCI 이미지를 빌드하기 위한 스프링부트 플러그인 작업
	imageName = "${project.name}"	// 빌드할 OCI 이미지의 이름. 이 이름은 그래들의 프로젝트 설정에서 정의한 이름과 같다. 버전 번호를 사용하지 않고 암묵적인 latest태그를 사용한다.
	environment = ["BP_JVM_VERSION": "17.*"]	// 이미지에 설치할 JVM 버전. 최신 버전인 17을 사용한다.
	// 카탈로그 서비스를 컨테이너화하기 위한 설정
	docker {	// 컨테이너 저장소 연결을 설정하기 위한 섹션
		publishRegistry {	// 컨테이너 저장소 인증을 설정하기 위한 섹션. 값은 그래들 속성을 통해 전달된다.	
		username = project.findProperty("registryUsername")
		password = project.findProperty("registryToken")
		url = project.findProperty("registryUrl")
		}
	}
}



dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.cloud:spring-cloud-starter-config'
	implementation 'org.springframework.retry:spring-retry'
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	// implementation 'org.flywaydb:flyway-core:9.22.3'
	implementation 'org.flywaydb:flyway-database-postgresql' //Flyway 10.x 버전 이상부터는 의존성 추가 시 사용 데이터베이스 명을 명시하여야 함
	
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
	testImplementation 'org.testcontainers:postgresql'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"	// 스프링 클라우드 의존성 관리를 위한 BOM
		mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"		// 테스트 컨테이너 의존성 관리를 위한 BOM
	}
}

bootRun {
	systemProperty 'spring.profiles.active', 'testdata'
}

tasks.named('test') {
	useJUnitPlatform()
}
